package forward_chain;

import com.ftn.sbnz.model.models.Lake;
import com.ftn.sbnz.model.models.HydroelectricPowerPlant;
import com.ftn.sbnz.model.models.Turbine;
import com.ftn.sbnz.model.events.TurbineOverheatingEvent;
import com.ftn.sbnz.model.events.DieselGeneratorOnEvent;
import com.ftn.sbnz.model.events.WaterFlowDecreasedEvent;
import com.ftn.sbnz.model.events.ElectricityProductionDecreasedEvent;


rule "Temperature over 25 C"
    when
        $l: Lake(waterTemp > 25)
        not (TurbineOverheatingEvent($l.id == lakeId))
        $h: HydroelectricPowerPlant()
    then
        for (Turbine turbine : $h.getTurbines()) {
            turbine.setOverheatingDanger(true);
        }
        insert(new TurbineOverheatingEvent($l.getId()));
        System.out.println("Temperature is over 25 degrees Celsius. Danger of turbine overheating is raised.");
end

rule "Danger of turbine overheating"
    when
        $t: TurbineOverheatingEvent()
        not (WaterFlowDecreasedEvent($t.lakeId == lakeId))
        $h: HydroelectricPowerPlant()
    then
        for (Turbine turbine : $h.getTurbines()) {
            turbine.setWaterFlow(turbine.getWaterFlow() * 0.5);
        }
        insert(new WaterFlowDecreasedEvent($t.getLakeId()));
        System.out.println("There is danger of turbine overheating. Turbine water flow will be decreased for 50%.");
end

rule "Water level decreased"
    when
        $w: WaterFlowDecreasedEvent()
        not (ElectricityProductionDecreasedEvent($w.lakeId == lakeId))
        $h: HydroelectricPowerPlant()
    then
        $h.setElectricityProduction($h.getElectricityProduction() * 0.5);
        insert(new ElectricityProductionDecreasedEvent($w.getLakeId()));
        System.out.println("Turbine water flow is decreased by 50%. Electricity production will be decreased by 50%.");
end

rule "Turn generators on"
    when
        $e: ElectricityProductionDecreasedEvent()
        $h: HydroelectricPowerPlant()
    then
        insert(new DieselGeneratorOnEvent());
        $h.setGeneratorsOn(true);
        System.out.println("Electricity production is decreased. Diesel generators will be turned on.");
end